plugins {
    id 'fabric-loom' version '1.1-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

configurations {
    library
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}


dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation "com.github.minechanjp:material-util:v1.1"
    modImplementation 'com.github.Vatuu:discord-rpc:1.6.2'
    modImplementation 'com.github.Dansoftowner:jSystemThemeDetector:3.6'

    library platform("org.lwjgl:lwjgl-bom:3.3.1")
    library "org.lwjgl:lwjgl-nanovg"

    compileOnly ('org.projectlombok:lombok:1.18.30')
    annotationProcessor('org.projectlombok:lombok:1.18.30')

    // lets just include ALL the natives!
    library "org.lwjgl:lwjgl-nanovg::natives-windows"
    library "org.lwjgl:lwjgl-nanovg::natives-macos"
    library "org.lwjgl:lwjgl-nanovg::natives-linux"
    library "org.lwjgl:lwjgl-nanovg::natives-macos-arm64"
    library "org.lwjgl:lwjgl-nanovg::natives-linux-arm64"
    library "org.lwjgl:lwjgl-nanovg::natives-linux-arm32"

    implementation configurations.library
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
